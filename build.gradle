subprojects { ->
    group = GROUP
    version = VERSION_NAME

    apply plugin: 'checkstyle'

    task checkstyle(type: Checkstyle) {
        configFile rootProject.file('checkstyle.xml')
        source 'src/main/java'
        ignoreFailures false
        showViolations true
        include '**/*.java'
        classpath = files()
    }

    afterEvaluate {
        if (project.getPlugins().hasPlugin('com.android.application') ||
                project.getPlugins().hasPlugin('com.android.library')) {
            preBuild.dependsOn('checkstyle')
        } else {
            compileJava.dependsOn('checkstyle')
        }
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://maven.google.com" }
    }

    buildscript {
        repositories {
            mavenCentral()
            maven {
                url "https://plugins.gradle.org/m2/"
            }
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:2.3.3'
            classpath 'gradle.plugin.com.kageiit:lintrules:1.1.2'
        }
    }
}

ext {
    minSdkVersion = 15
    targetSdkVersion = 26
    compileSdkVersion = 26
    buildToolsVersion = '26.0.2'
    sourceCompatibilityVersion = JavaVersion.VERSION_1_7
    targetCompatibilityVersion = JavaVersion.VERSION_1_7

}

ext.deps = [
        android           : 'com.google.android:android:4.1.1.4',
        supportcompat     : 'com.android.support:support-compat:26.0.2',
        supportAnnotations: 'com.android.support:support-annotations:26.0.2',
        appcompat         : 'com.android.support:appcompat-v7:26.1.0',
        javapoet          : 'com.squareup:javapoet:1.9.0',
        rxandroid         : 'io.reactivex:rxandroid:1.2.1',
        rxjava            : 'io.reactivex:rxjava:1.3.2',
        compiletesting    : 'com.google.testing.compile:compile-testing:0.12',
        robolectric       : 'org.robolectric:robolectric:3.4.2',
        mockito           : 'org.mockito:mockito-core:1.+',
        junit             : 'junit:junit:4.12',
        autoservice       : 'com.google.auto.service:auto-service:1.0-rc2',
        autocommon        : 'com.google.auto:auto-common:0.6',
        lint              : 'com.android.tools.lint:lint:26.0.0-beta6',
        lintapi           : 'com.android.tools.lint:lint-api:26.0.0-beta6',
        lintchecks        : 'com.android.tools.lint:lint-checks:26.0.0-beta6',
        linttests         : 'com.android.tools.lint:lint-tests:26.0.0-beta6'
]

task clean(type: Delete) {
    delete rootProject.buildDir
}