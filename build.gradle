subprojects { ->
    group = GROUP
    version = VERSION_NAME

    apply plugin: 'checkstyle'

    task checkstyle(type: Checkstyle) {
        configFile rootProject.file('checkstyle.xml')
        source 'src/main/java'
        ignoreFailures false
        showViolations true
        include '**/*.java'
        classpath = files()
    }

    afterEvaluate {
        if (project.getPlugins().hasPlugin('com.android.application') ||
                project.getPlugins().hasPlugin('com.android.library')) {
            preBuild.dependsOn('checkstyle')
        } else {
            compileJava.dependsOn('checkstyle')
        }
    }

    repositories {
        mavenCentral()
    }

    buildscript {
        repositories {
            mavenCentral()
            maven {
                url "https://plugins.gradle.org/m2/"
            }
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:2.2.2'
            classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
            classpath 'gradle.plugin.com.kageiit:lintrules:1.1.2'
        }
    }
}

ext {
    minSdkVersion = 9
    targetSdkVersion = 24
    compileSdkVersion = 24
    buildToolsVersion = '24.0.1'
    sourceCompatibilityVersion = JavaVersion.VERSION_1_7
    targetCompatibilityVersion = JavaVersion.VERSION_1_7

}

ext.deps = [
        android           : 'com.google.android:android:2.1.2',
        supportv4         : 'com.android.support:support-v4:24.2.0',
        supportAnnotations: 'com.android.support:support-annotations:24.2.0',
        javapoet          : 'com.squareup:javapoet:1.8.0',
        rxandroid         : 'io.reactivex:rxandroid:1.2.1',
        rxjava            : 'io.reactivex:rxjava:1.1.6',
        appcompat         : 'com.android.support:appcompat-v7:24.2.0',
        compiletesting    : 'com.google.testing.compile:compile-testing:0.9',
        robolectric       : 'org.robolectric:robolectric:3.1',
        mockito           : 'org.mockito:mockito-core:1.+',
        junit             : 'junit:junit:4.12',
        autoservice       : 'com.google.auto.service:auto-service:1.0-rc2',
        autocommon        : 'com.google.auto:auto-common:0.6',
        lint              : 'com.android.tools.lint:lint:24.3.1',
        lintapi           : 'com.android.tools.lint:lint-api:24.3.1',
        lintchecks        : 'com.android.tools.lint:lint-checks:24.3.1',
        linttests         : 'com.android.tools.lint:lint-tests:24.3.1'
]

task clean(type: Delete) {
    delete rootProject.buildDir
}